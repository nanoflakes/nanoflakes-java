plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group = 'com.github.nanoflakes'
version = '1.0'

sourceCompatibility = 1.8

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version project.version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ['BintrayRelease']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'nanoflakes'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nanoflakes/nanoflakes-java'
        publish = true
        version {
            name = project.version
        }
    }
}

bintrayUpload {
    dependsOn build
    dependsOn publishToMavenLocal
}
